# $Id: PKGBUILD,v 1.28 2009/03/26 10:23:45 sergej Exp $
# Maintainer: Sergej Pupykin <sergej@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Orivej Desh <smpuj@bk.ru>
# Contributor: Nicolas Doualot <public@slubman.info>

pkgname=fbreader-qt
pkgver=0.10.5
pkgrel=1
pkgdesc="An e-book reader for Linux"
arch=('i686' 'x86_64')
url="http://www.fbreader.org/"
license=('GPL')
provides=('fbreader')
conflicts=('fbreader')
depends=(fribidi bzip2 curl qt)
source=(http://only.mawhrin.net/fbreader/fbreader-sources-$pkgver.tgz \
	http://archlinux-stuff.googlecode.com/files/liblinebreak-20080321.tar.gz
	desktop.mk.patch)	

build() {
  cd ${srcdir}/liblinebreak-20080321
  make CFLAGS=-fPIC || return 1

  #export CPPFLAGS="-I/usr/include/cairo"
  export TARGET_ARCH=desktop 
  export UI_TYPE=qt4
  export TARGET_STATUS=release

  cd ${srcdir}/fbreader-$pkgver

patch -p0 makefiles/config.mk <<EOF
diff -wbBur fbreader-0.8.17/makefiles/config.mk fbreader-0.8.17.my/makefiles/config.mk
--- fbreader-0.8.17/makefiles/config.mk	2008-03-22 16:00:31.000000000 +0300
+++ fbreader-0.8.17.my/makefiles/config.mk	2008-03-24 21:01:30.000000000 +0300
@@ -12,11 +12,13 @@
 SHAREDIR ?= \$(INSTALLDIR)/share
 BASEDIR ?= \$(SHAREDIR)
 
-CFLAGS += -DINSTALLDIR=\"\$(INSTALLDIR)\" -DBASEDIR=\"\$(BASEDIR)\" -DLIBDIR=\"\$(LIBDIR)\" -DIMAGEDIR=\"\$(IMAGEDIR)\" -DAPPIMAGEDIR=\"\$(APPIMAGEDIR)\" -DVERSION=\"\$(VERSION)\"
+CFLAGS += -I${srcdir}/liblinebreak-20080321 -DINSTALLDIR=\"\$(INSTALLDIR)\" -DBASEDIR=\"\$(BASEDIR)\" -DLIBDIR=\"\$(LIBDIR)\" -DIMAGEDIR=\"\$(IMAGEDIR)\" -DAPPIMAGEDIR=\"\$(APPIMAGEDIR)\" -DVERSION=\"\$(VERSION)\"
 ifeq "\$(ZLSHARED)" "yes"
   CFLAGS += -fPIC -DZLSHARED
 endif
 
+LDFLAGS += -L${srcdir}/liblinebreak-20080321/DebugDir
+
 ifeq "\$(TARGET_STATUS)" "release"
 	CFLAGS += -O3
 	LDFLAGS += -s
EOF
  patch -p1 -i ../desktop.mk.patch

  make INSTALLDIR=/usr || return 1
  make INSTALLDIR=/usr DESTDIR=${pkgdir} install
}

md5sums=('5033e1116e4c240a2b4b58b21f2bfbdd'
         'adec1484446029222dcedcd12f7e635e'
         '35b7c625e46ef1ac499b58dad0144eb0')
sha256sums=('1357c557bd417fd6ddb077b6649fca90c2bb7964c278734af39e101296a01283'
            '42c24eccf96aa84e7c434501122d26c42c215abf9faa787bbdce9245c905c645'
            '54fae5d83c75965958f8b3e8ddabaeb5e2590c4e8d491a7dcbf07ac4e6e1e6fd')
