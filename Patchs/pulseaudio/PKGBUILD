# $Id: PKGBUILD,v 1.19 2008/04/04 01:05:44 bardo Exp $
# vim:set ts=2 sw=2 et:
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pulseaudio
pkgver=0.9.12
pkgrel=5
pkgdesc="A networked sound server"
arch=('i686' 'x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=('hal' 'libasyncns' 'libcap' 'liboil' 'libsamplerate' 'libtool')
makedepends=('autoconf' 'avahi' 'bluez-libs' 'gconf' 'jack-audio-connection-kit'
             'libatomic_ops' 'lirc-utils' 'pkgconfig' 'tcp_wrappers')
provides=('polypaudio')
conflicts=('polypaudio')
replaces=('polypaudio')
options=('emptydirs' '!libtool' '!makeflags')
backup=('etc/pulse/client.conf' 'etc/pulse/daemon.conf' 'etc/pulse/default.pa' 'etc/pulse/system.pa' 'etc/conf.d/pulseaudio')
install=pulseaudio.install
source=(http://0pointer.de/lennart/projects/pulseaudio/${pkgname}-${pkgver}.tar.gz
        pulseaudio.conf pulseaudio.rc)

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  
  # Necessary to compile as of 0.9.10
  autoreconf -fi || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-lynx --disable-ltdl-install

  make || return 1
  make DESTDIR=${startdir}/pkg install

  install -Dm755 ${startdir}/src/pulseaudio.rc ${startdir}/pkg/etc/rc.d/pulseaudio || return 1
  install -Dm644 ${startdir}/src/pulseaudio.conf ${startdir}/pkg/etc/conf.d/pulseaudio || return 1
  install -d ${startdir}/pkg/var/run/pulse || return 1
}

md5sums=('b8851d52152c5c3b65f4f31ee1ab7631'
         'd4a7d4ad51b406588ba7ac7931c5dd88'
         '87f6360dc6b3e367930dc8f66b0dc5b1')
sha256sums=('1f3740b96c062a9718cb5f5e2c37cf3b6e4d895fdf66a64b7bc686b249d6eafc'
            '40757a3b83ccce0122ff0520e8c4ba201da46eb87180ba27e6f78c81087b4f5a'
            'bdc72aaa744a33f7da5b5db0295e788bf85f3fa1a4ed5d1819850866d640118c')
