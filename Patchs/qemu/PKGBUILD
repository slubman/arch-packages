# $Id: PKGBUILD 7781 2008-08-04 09:00:50Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=qemu
pkgver=0.10.1
pkgrel=3
_kvmver=84
pkgdesc="QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL')
url="http://www.nongnu.org/qemu"
depends=('sdl' 'alsa-lib' 'zlib' 'e2fsprogs' 'gnutls>=2.0.4')
makedepends=('pulseaudio')
install=qemu.install
source=(http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz
        70-kqemu.rules \
        http://downloads.sourceforge.net/kvm/kvm-$_kvmver.tar.gz\
	kvm-bios.diff)

build()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  unset CFLAGS
  sed -i -e 's/lib64/lib/g' x86_64.ld || return 1
  ./configure --prefix=/usr --audio-drv-list=oss,alsa,sdl,pa
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 ${srcdir}/70-kqemu.rules \
                   ${pkgdir}/etc/udev/rules.d/70-kqemu.rules

  # we add the kvm/qemu version
    cd ${srcdir}/kvm-$_kvmver
    patch -Np1 -i ../kvm-bios.diff || return 1
    ./configure --prefix=/usr --audio-drv-list=oss,alsa,sdl,pa
  # fix sdl compilation, JON: and extboot
    sed -i -e 's#-rpath,/usr/lib#-rpath,/usr/lib,-rpath,/lib#g' qemu/config-host.mak
    for dir in libkvm user qemu extboot; do
      cd ${srcdir}/kvm-$_kvmver/${dir}
      make || return 1
    done
   # install kvm-qemu
    install -m 755 ${srcdir}/kvm-$_kvmver/qemu/x86_64-softmmu/qemu-system-x86_64 \
        ${pkgdir}/usr/bin/qemu-kvm
    install -D -m644 ${srcdir}/kvm-$_kvmver/scripts/65-kvm.rules \
    	${pkgdir}/etc/udev/rules.d/65-kvm.rules || return 1
   # install kvm bios files
   install -D -m644 ${srcdir}/kvm-$_kvmver/qemu/pc-bios/bios.bin \
       ${pkgdir}/usr/share/qemu/bios-kvm.bin
   install -D -m644 ${srcdir}/kvm-$_kvmver/qemu/pc-bios/vgabios.bin \
       ${pkgdir}/usr/share/qemu/vgabios-kvm.bin
   install -D -m644 ${srcdir}/kvm-$_kvmver/qemu/pc-bios/vgabios-cirrus.bin \
       ${pkgdir}/usr/share/qemu/vgabios-cirrus-kvm.bin
   install -D -m644 ${srcdir}/kvm-$_kvmver/extboot/extboot.bin \
	${pkgdir}/usr/share/qemu/extboot-kvm.bin
}

md5sums=('8dc50b834fa3f5f6a17d7bc3d0559e53'
         'ec06591830b8fcf53913f05f3d66f7e5'
         '1cdfcbd3cc569eec96a36d7557291fd2')
sha256sums=('3c5ecf320996ad243b7adc4798ad5dbfd58a6d5562cc0317a4cde7c7fb3edbb2'
            'f68a919d5c99252759e5349cc573a44a0619e20e29d97b6a0f26d6861deed41c'
            '78f4994db9161fd7c5384874a47802c03e5d2c0b4c4b8d75c3c687bb6a1d2f64')
