# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor : Nicolas Doualot <packages@slubman.info>

pkgname=qemu
pkgver=0.10.3
pkgrel=2
_kvmver=85
pkgdesc="QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL' 'LGPL2')
url="http://www.nongnu.org/qemu"
depends=('sdl' 'alsa-lib' 'zlib' 'e2fsprogs' 'gnutls>=2.0.4')
makedepends=('pulseaudio')
install=qemu.install
source=(http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz
        70-kqemu.rules \
        http://downloads.sourceforge.net/kvm/kvm-$_kvmver.tar.gz\
	kvm-bios.diff)

build()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  unset CFLAGS
  ./configure --prefix=/usr --audio-drv-list=oss,alsa,sdl,pa \
              --kerneldir="/usr/src/linux-$(uname -r)"
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 ${srcdir}/70-kqemu.rules \
                   ${pkgdir}/lib/udev/rules.d/70-kqemu.rules

  # we add the kvm/qemu version
    cd ${srcdir}/kvm-$_kvmver
    patch -Np1 -i ../kvm-bios.diff || return 1
    ./configure --prefix=/usr --audio-drv-list=oss,alsa,sdl,pa --with-patched-kernel
    make || return 1
   # install kvm-qemu as qemu-kvm
    install -m 755 ${srcdir}/kvm-$_kvmver/qemu/x86_64-softmmu/qemu-system-x86_64 \
        ${pkgdir}/usr/bin/qemu-kvm
    install -D -m644 ${srcdir}/kvm-$_kvmver/scripts/65-kvm.rules \
    	${pkgdir}/lib/udev/rules.d/65-kvm.rules || return 1
   # install kvm bios files
   install -D -m644 ${srcdir}/kvm-$_kvmver/qemu/pc-bios/bios.bin \
       ${pkgdir}/usr/share/qemu/bios-kvm.bin
   install -D -m644 ${srcdir}/kvm-$_kvmver/qemu/pc-bios/vgabios.bin \
       ${pkgdir}/usr/share/qemu/vgabios-kvm.bin
   install -D -m644 ${srcdir}/kvm-$_kvmver/qemu/pc-bios/vgabios-cirrus.bin \
       ${pkgdir}/usr/share/qemu/vgabios-cirrus-kvm.bin
   install -D -m644 ${srcdir}/kvm-$_kvmver/extboot/extboot.bin \
	${pkgdir}/usr/share/qemu/extboot-kvm.bin
}

md5sums=('320d6c536c8a1288cdc6c809f487e6d7'
         'ec06591830b8fcf53913f05f3d66f7e5'
         '7eec5cec0368e098f11ced03395e7439'
         '1cdfcbd3cc569eec96a36d7557291fd2')
sha256sums=('89540d7a8c880a6ae33f8a55d55d580f3f03c10b02e32b7323719b3354f7b077'
            'f68a919d5c99252759e5349cc573a44a0619e20e29d97b6a0f26d6861deed41c'
            '628601051997677ab4501903b366519a47b1cf16f6ce6f8871a12da478a0c6ad'
            '78f4994db9161fd7c5384874a47802c03e5d2c0b4c4b8d75c3c687bb6a1d2f64')
