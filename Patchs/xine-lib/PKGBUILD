# $Id: PKGBUILD 20666 2008-12-06 08:58:36Z allan $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=xine-lib
pkgver=1.1.16.2
pkgrel=1
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
url="http://xinehq.de/"
license=('LGPL' 'GPL')
depends=('libgl' 'libxvmc' 'esound' 'flac>=1.1.4' 'libvorbis' 'sdl' 'libmng' 'libtheora'
         'libxcb' 'wavpack' 'ffmpeg>=20081220' 'heimdal>=1.2.1')
makedepends=('pkgconfig' 'libtool' 'imagemagick' 'smbclient' 'mesa' 'alsa-lib' 
             'vcdimager' 'pulseaudio')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.bz2
        xine-lib-1.1.1-configure-no-mcpu-march.patch)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 < ../xine-lib-1.1.1-configure-no-mcpu-march.patch || return 1

  libtoolize --force --copy || return 1
  aclocal -I m4 || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr --with-w32-path=/usr/lib/codecs \
    --with-xv-path=/usr/lib --with-xxmc-path=/usr/lib --with-xvmc-path=/usr/lib \
    --with-libflac --with-wavpack --with-xcb \
    --without-arts --without-jack --without-speex \
    --disable-gnomevfs --with-pulseaudio --enable-aalib \
    --disable-modplug --with-external-ffmpeg || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}

md5sums=('336dd1f6266e9f943430ca6009b7aa7e'
         '9776df4eb54d2f1f68d8268adbc3b5c2')
sha256sums=('67f2469faee964b75f43459547b5e2f9a407b8dc30cbeffa989cce312e16b2c1'
            '9f37e23a5a83dff5264e647d62c76db7a74adb74ee4f0bc6666ca4e93fda8757')
