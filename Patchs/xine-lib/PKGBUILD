# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=xine-lib
pkgver=1.1.18
pkgrel=2
pkgdesc="A multimedia playback engine"
arch=('i686' 'x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libgl' 'libxvmc' 'esound' 'flac' 'libvorbis' 'sdl' 'libmng' 'libtheora'
         'libxcb' 'wavpack' 'ffmpeg' 'heimdal>=1.2.1' 'faad2' 'aalib')
makedepends=('pkgconfig' 'libtool' 'imagemagick' 'smbclient' 'mesa' 'alsa-lib' 
             'vcdimager' 'pulseaudio')
optdepends=('imagemagick: for using the imagemagick plugin' 'smbclient: for using the smb plugin' 'pulseaudio: for using the pulseaudio plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.bz2
	http://downloads.sourceforge.net/sourceforge/xine/compat.c
        xine-lib-1.1.1-configure-no-mcpu-march.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp "${srcdir}/compat.c" src/dxr3/compat.c || return 1
  patch -p0 < ../xine-lib-1.1.1-configure-no-mcpu-march.patch || return 1

  libtoolize --force --copy || return 1
  aclocal -I m4 || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr --with-w32-path=/usr/lib/codecs \
    --with-xv-path=/usr/lib --with-xxmc-path=/usr/lib --with-xvmc-path=/usr/lib \
    --with-libflac --with-wavpack --with-xcb \
    --without-arts --without-jack --without-speex \
    --disable-gnomevfs --with-pulseaudio --enable-aalib \
    --disable-modplug --with-external-ffmpeg || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}

md5sums=('2313c279bc99303cd12d4ee0ac826e20'
         'c4537f3b0445e15a031e1f69fc57bc28'
         '9776df4eb54d2f1f68d8268adbc3b5c2')
sha256sums=('4ba164c7a192b265a7c72bd623bd69b261bcf7716ecca90a08d67025dd16bfb7'
            'a9ec7fcad69b99e7839f78d72b7f615d1f505ef112f1f8cd397e58f49116079c'
            '9f37e23a5a83dff5264e647d62c76db7a74adb74ee4f0bc6666ca4e93fda8757')
